import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'me.kaliber'
version '1.0'

repositories {
    mavenCentral()

    maven {
        url 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        url 'https://repo.extendedclip.com/content/repositories/placeholderapi'
    }
}

dependencies {
    // kotlin
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'

    // command framework
    implementation 'me.mattstudios.utils:matt-framework:1.4.6'

    // adventure api
    implementation 'net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT'

    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.9'
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

shadowJar {
    minimize()
    relocate 'kotlin', 'me.kaliber.libs.kotlin'
    relocate 'me.mattstudios.mf', 'me.kaliber.libs.cmd'
    relocate 'net.kyori', 'me.kaliber.libs.adventure'
    archiveFileName = "CombatStats-${project.version}.jar"
}
